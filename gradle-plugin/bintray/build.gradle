#!/usr/bin/env groovy
/*
 * Build script for gradle-about-plugin
 * Copyright Â©  Basil Peace
 *
 * This file is part of gradle-about-plugin.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
plugins {
  id 'org.fidata.project.java'
  id 'org.fidata.project.groovy'
  id 'org.fidata.plugin'
}
publicReleases = true // TODO

ext.rootDir = file('.')

dependencies {
  implementation(project(':gradle-plugin:maven'))
  implementation(group: 'com.jfrog.bintray', name: 'com.jfrog.bintray.gradle.plugin', version: '[1, 2[') {
    /*
     * WORKAROUND:
     * org.apache.maven:maven-ant-tasks has old plexus dependency which have undesired JUnit dependency
     * <grv87 2018-06-24>
     */
    exclude group: 'org.codehaus.plexus', module: 'plexus-container-default'
  }
  implementation(group: 'org.codehaus.plexus', name: 'plexus-container-default', version: '[1, 2[') {
    /*
     * WORKAROUND:
     * org.codehaus.plexus:plexus-container-default still has google-collections dependency
     * which is superseded by Guava
     * <grv87 2018-06-24>
     */
    exclude group: 'com.google.collections', module: 'google-collections'
  }
}

apply from: new File(ext.rootDir, 'gradle/gradle-plugin-markers.gradle')

stutter {
  java(8) {
    compatibleRange '2.10'
  }
  java(9) {
    compatibleRange '2.10'
  }
}

tasks.named('codenarcCompatTest').configure {
  disabledRules.add 'JavaIoPackageAccess' // TODO
}
pluginBundle.plugins {
  aboutBintrayPlugin {
    displayName = 'Integration plugin between About and Bintray plugins'
  }
}
